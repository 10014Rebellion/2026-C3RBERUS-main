import java.nio.file.Files

plugins {
    id "java"
    id "edu.wpi.first.GradleRIO" version "2026.2.1"
    id "com.peterabeles.gversion" version "1.10"
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

def ROBOT_MAIN_CLASS = "frc.robot.Main"
def telemetryKeysJava = file("src/main/java/frc/robot/logging/TelemetryKeys.java")
def dashboardDeployDir = file("src/main/deploy/dashboard")
def telemetryKeysJsOut = file("${dashboardDeployDir}/telemetryKeys.js")



// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project DeployUtils.
deploy {
    targets {
        roborio(getTargetTypeClass('RoboRIO')) {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = project.frc.getTeamNumber()
            debug = project.frc.getDebugOrDefault(false)

            artifacts {
                // First part is artifact name, 2nd is artifact type
                // getTargetTypeClass is a shortcut to get the class type using a string

                frcJava(getArtifactTypeClass('FRCJavaArtifact')) {
                    jvmArgs.add("-XX:+UnlockExperimentalVMOptions")
                    jvmArgs.add("-XX:GCTimeRatio=5")
                    jvmArgs.add("-XX:+UseSerialGC")
                    jvmArgs.add("-XX:MaxGCPauseMillis=50")

                    // The options below may improve performance, but should only be enabled on the RIO 2
                    //
                    final MAX_JAVA_HEAP_SIZE_MB = 100;
                    jvmArgs.add("-Xmx" + MAX_JAVA_HEAP_SIZE_MB + "M")
                    jvmArgs.add("-Xms" + MAX_JAVA_HEAP_SIZE_MB + "M")
                    jvmArgs.add("-XX:+AlwaysPreTouch")
                }

                // Static files artifact
                frcStaticFileDeploy(getArtifactTypeClass('FileTreeArtifact')) {
                    files = project.fileTree('src/main/deploy')
                    directory = '/home/lvuser/deploy'
                    deleteOldFiles = false // Change to true to delete files on roboRIO that no
                                           // longer exist in deploy directory of this project
                }
            }
        }
    }
}

def deployArtifact = deploy.targets.roborio.artifacts.frcJava

// Set to true to use debug for all targets including JNI, which will drastically impact
// performance.
wpi.java.debugJni = false

// Set this to true to enable desktop support.
def includeDesktopSupport = true

// Configuration for AdvantageKit
task(replayWatch, type: JavaExec) {
    mainClass = "org.littletonrobotics.junction.ReplayWatch"
    classpath = sourceSets.main.runtimeClasspath
}

// Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
// Also defines JUnit 5.
dependencies {
    annotationProcessor wpi.java.deps.wpilibAnnotations()
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()

    roborioDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.roborio)
    roborioDebug wpi.java.vendor.jniDebug(wpi.platforms.roborio)

    roborioRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.roborio)
    roborioRelease wpi.java.vendor.jniRelease(wpi.platforms.roborio)

    nativeDebug wpi.java.deps.wpilibJniDebug(wpi.platforms.desktop)
    nativeDebug wpi.java.vendor.jniDebug(wpi.platforms.desktop)
    simulationDebug wpi.sim.enableDebug()

    nativeRelease wpi.java.deps.wpilibJniRelease(wpi.platforms.desktop)
    nativeRelease wpi.java.vendor.jniRelease(wpi.platforms.desktop)
    simulationRelease wpi.sim.enableRelease()

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    def akitJson = new groovy.json.JsonSlurper().parseText(new File(projectDir.getAbsolutePath() + "/vendordeps/AdvantageKit.json").text)
    annotationProcessor "org.littletonrobotics.akit:akit-autolog:$akitJson.version"
}

test {
    useJUnitPlatform()
    systemProperty 'junit.jupiter.extensions.autodetection.enabled', 'true'
}

// Simulation configuration (e.g. environment variables).
wpi.sim.addGui().defaultEnabled = true
wpi.sim.addDriverstation()

// Setting up my Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class.
jar {
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    from('src') { into 'backup/src' }
    from('vendordeps') { into 'backup/vendordeps' }
    from('build.gradle') { into 'backup' }
    manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_MAIN_CLASS)
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

// Configure jar and deploy tasks
deployArtifact.jarTask = jar
wpi.java.configureExecutableTasks(jar)
wpi.java.configureTestTasks(test)

// Configure string concat to always inline compile
tasks.withType(JavaCompile) {
    options.compilerArgs.add '-XDstringConcat=inline'
}

// Create version file
project.compileJava.dependsOn(createVersionFile)
gversion {
    srcDir       = "src/main/java/"
    classPackage = "frc.robot"
    className    = "BuildConstants"
    dateFormat   = "yyyy-MM-dd HH:mm:ss z"
    timeZone     = "America/Chicago"
    indent       = "  "
}

// Generate dashboard telemetryjava
tasks.register("generateTelemetryKeysJs") {
    inputs.file(telemetryKeysJava)
    outputs.file(telemetryKeysJsOut)

    doLast {
        if (!telemetryKeysJava.exists()) {
            throw new GradleException("TelemetryKeys.java not found at: ${telemetryKeysJava}")
        }

        dashboardDeployDir.mkdirs()
        def text = telemetryKeysJava.getText("UTF-8")

        def tableMatch = (text =~ /public\s+static\s+final\s+String\s+TABLE\s*=\s*"([^"]+)";/)
        if (!tableMatch.find()) {
            throw new GradleException("Could not find TABLE in TelemetryKeys.java")
        }
        def table = tableMatch.group(1)

        def stringMatches = (text =~ /public\s+static\s+final\s+String\s+([A-Z0-9_]+)\s*=\s*"([^"]+)";/)
        def doubleMatches = (text =~ /public\s+static\s+final\s+double\s+([A-Z0-9_]+)\s*=\s*([-+]?\d*\.?\d+(?:[eE][-+]?\d+)?);/)
        def intMatches = (text =~ /public\s+static\s+final\s+int\s+([A-Z0-9_]+)\s*=\s*([0-9]+);/)
        def booleanMatches = (text =~ /public\s+static\s+final\s+boolean\s+([A-Z0-9_]+)\s*=\s*(true|false);/)

        def js = new StringBuilder()
        js.append("// AUTO-GENERATED from TelemetryKeys.java. DO NOT EDIT.\n")
        js.append("export const TABLE = \"${table}\";\n\n")

        js.append("export const KEYS = {\n")
        stringMatches.each { m ->
            def name = m[1]
            def value = m[2]
            if (name != "TABLE") {
                js.append("  ${name}: \"${value}\",\n")
            }
        }
        js.append("};\n\n")

        js.append("export const CONST = {\n")
        doubleMatches.each { m -> js.append("  ${m[1]}: ${m[2]},\n") }
        intMatches.each { m -> js.append("  ${m[1]}: ${m[2]},\n") }
        booleanMatches.each { m -> js.append("  ${m[1]}: ${m[2]},\n") }
        js.append("};\n")

        telemetryKeysJsOut.write(js.toString(), "UTF-8")
        println("Generated: ${telemetryKeysJsOut}")
    }
}

tasks.named("processResources") { dependsOn("generateTelemetryKeysJs") }
tasks.matching { it.name.toLowerCase().contains("deploy") }.configureEach {
    dependsOn("generateTelemetryKeysJs")
}